#*******************************************
# MacDescription = Add waypoint(s) to permanent delete list for this database
# MacFileName =Add to Delete List.gsk
# MacAuthor = Kai Team
# MacVersion=1.51
# MacUrl=http://gsak.net/board/index.php?showtopic=6712&view=findpost&p=40203
#*******************************************
# Initialize variables
$WaypointsAdded=0
$Skipped=0
$Added=0
$DeleteFile=""
$Data=""
$Values=""
$Dups=0
$DataFolder = Trim(Sysinfo("gsakini;TfmConfig;edtDataStore.Text"))
# Allow for data folder with and without trailing slash
If Right($DataFolder,1) = "\"
 $slash = ""
else
 $slash = "\"
EndIf
$DBList=$_CurrentDatabase + ";" + Replace($_CurrentDatabase + ";","",Sysinfo("databases"),TRUE)

#Form to select the database:
WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
            CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
            GOSUB Name=Run
            BREAK

        CASE $FormExit = "Cancel"
            CANCEL Msg="Macro Cancelled"
    ENDCASE
ENDWHILE

BEGINSUB Name=Run
    $Database=$ComboBox1
    IF $_Version>="7.7.0.67"
        $File=$DataFolder + $slash + $Database + "\Sqlite.db3"
    ELSE
        $File = $DataFolder + $slash + $Database + "\delete.txt"
        # Check if permanent delete list exists for this database,
        # and copy contents into $DeleteFile variable if it does exist
        IF FileExists($File)
            $DeleteFile = GetFile($File)
            IF Left($DeleteFile,7) = "*Error*"
                MSGOK Msg="$DeleteFile"
                CANCEL
            ENDIF
        ENDIF
    ENDIF

    # Iterate through the waypoints, adding code and name to permanent delete list
    # if not already in the permanent delete list for this database.
    IF $_Version>="7.7.0.67"
        $Added=$_Count
        $status = sqlite("sql",$_SqlGrid, "sqlget=yes")
        WHILE NOT($_sqleol)
            $ThisCode=SqlGet("Code")
            $ThisName=SqlQuote(SqlGet("Name"))
            $Values = $Values + "'" + $ThisCode + "'" + "," + $ThisName + $_NewLine
            SQLNEXT
        ENDWHILE
        #open the selected database (may not be the current database)
        $tempS=Sqlite("Open","$File")
        $Count=RegExCount($_NewLine,$Values)
        $x=1
        TRANSACTION Action=Begin
        WHILE $x<=$Count
            $data=Extract($Values,$_NewLine,$x)
            $Code=Extract($data,",",1)
            $_sql="Select Count(*) from Ignore WHERE iCode=$Code"
            $Dups=$Dups + Val(Sqlite("sql",$_sql))
            $_sql="Replace into Ignore Values($data)"
            $update=Sqlite("sql",$_sql)
            $x=$x+1
        ENDWHILE
        TRANSACTION Action=End
        $tempS=Sqlite("Close","","")
    ELSE
        GOTO Position=Top
        WHILE NOT($_EOL)
            IF RegExCount($d_Code, $DeleteFile)>0
                $Skipped=$Skipped + 1
                GOTO Position=Next
            ELSE
                $Data=$Data + $d_Code + " " + $d_Name + $_NewLine
                $Added=$Added + 1
                GOTO Position=Next
            ENDIF
        ENDWHILE

        #If Permanet Delete file exists, append new additions, otherwise, create the file.
        IF FileExists($File)
            $Output=Appendfile($File, $Data)
            IF Left($Output,7) = "*Error*"
                MSGOK Msg="$Output"
                CANCEL
            ENDIF
        ELSE
            $Output=PutFile($File, $Data)
            IF Left($Output,7) = "*Error*"
                MSGOK Msg="$Output"
                CANCEL
            ENDIF
        ENDIF
    ENDIF

    #Convert waypoint counts to strings
    IF $_Version>="7.7.0.67"
        $Added=$Added-$Dups
        $AddedString=NumToStr($Added)
        $SkippedString=NumToStr($Dups)
    ELSE
        $AddedString=NumToStr($Added)
        $SkippedString=NumToStr($Skipped)
    ENDIF
    #Tell the user how many waypoints were added to the permanent delete list.
    MSGOK msg="$AddedString waypoint(s) added to the permanenet delete list for $database and $SkippedString waypoint(s) already on the list."
ENDSUB

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 15-Nov-2008 19:22:40
#********************************************************************

Name = Form1
  Type = Form
  Caption = Add to Permanent Delete List
  Height = 152
  Width = 374

Name = Label1
  Type = Label
  Height = 16
  Left = 24
  Size = 10
  Top = 10
  Width = 304
  Caption = Add these waypoints to the permanent delete list for

Name = Combobox1
  Type = Combobox
  Height = 21
  Left = 169
  Top = 30
  Values = $DBList
  Width = 161
  Taborder = 8

Name = OK
  Type = Button
  Height = 25
  Left = 79
  Top = 73
  Width = 75
  Taborder = 9
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 211
  Top = 73
  Width = 75
  Taborder = 10
  Caption = Cancel

Name = Label2
  Type = Label
  Height = 16
  Left = 24
  Size = 10
  Top = 32
  Width = 137
  Caption = the following database:

<enddata>


