#************************************************************
# MacDescription = Show your finds in distance "bands" around your currently selected location.
# MacFileName = DistanceBands.gsk
# MacAuthor = Lil Devil
# MacVersion = 1.0
#************************************************************

VERCHECK Version=7.7.0.0

# defaults
$bandSize = 100
$minDistance = 0
$maxDistance = 2700
$includeQuantity = false
$includePercent = false
$includeCache = true

MACSETTINGS Type=R Filecheck=N

$bandSizeStr = NumToStr($bandSize)
$minDistanceStr = NumToStr($minDistance)
$maxDistanceStr = NumToStr($maxDistance)

IF Sysinfo("Distance") = "M"
	$units = "miles"
ELSE
	$units = "kilometers"
ENDIF
$form = EditForm($form, "bandSizeUnits",    "Caption", $units)
$form = EditForm($form, "minDistanceUnits", "Caption", $units)
$form = EditForm($form, "maxDistanceUnits", "Caption", $units)

WHILE true
	$FormExit = Form($form,"")

	$bandSize = Val($bandSizeStr)
	$minDistance = Val($minDistanceStr)
	$maxDistance = Val($maxDistanceStr)

	BEGINCASE
	CASE $FormExit = "runButton"
		MACSETTINGS Type=S Vars=bandSize,minDistance,maxDistance,includeQuantity,includePercent,includeCache
		GOSUB Name=Run
		RETURN

	OTHERWISE
		RETURN
	ENDCASE
ENDWHILE

BEGINSUB Name=Run
	$output = "Band"
	IF $includeQuantity
		$output = $output + ";Quantity"
	ENDIF
	IF $includePercent
		$output = $output + ";Percent"
		$totalCaches = Val(Sqlite("sql", "SELECT Count(*) FROM CachesAll WHERE Found", ""))
	ENDIF
	IF $includeCache
		$output = $output + ";" + Upper(Left($units,1)) + Substr($units,2,0) + ";Date Found;GC Code;Cache Name"
	ENDIF
	$output = $output + $_CrLf

	$bandStart = $minDistance
	$bandEnd = $bandSize

	REPEAT
		SHOWSTATUS Msg=Processing band $bandStart-$bandEnd
		GOSUB Name=Band
		$bandStart = $bandStart + $bandSize
		$bandEnd = $bandEnd + $bandSize
	UNTIL $bandStart >= $maxDistance

	$html = SqlToHtml($output, "Distance Bands", "y")

	$output = RegExReplace(";", $output, " - ")
	CLIP Data=$output
ENDSUB

BEGINSUB Name=Band
	$output = $output + "$bandStart-$bandEnd"

	IF $includeQuantity OR $includePercent
		$bandCount = Val(Sqlite("sql", "SELECT Count(*) FROM CachesAll WHERE Found AND Distance>=$bandStart AND Distance<$bandEnd", ""))
	ENDIF

	IF $includeQuantity
		$output = $output + ";$bandCount"
	ENDIF

	IF $includePercent
		$output = $output + ";" + Str(($bandCount/$totalCaches)*100, 3, 1)
	ENDIF

	IF $includeCache
		$bandMiddle = ($bandStart + $bandEnd) / 2
		$cacheData = Sqlite("sql", "SELECT CAST(round(Distance) AS Integer),g_DateFormat(FoundByMeDate),Code,Name FROM CachesAll WHERE Found AND Distance>=$bandStart AND Distance<$bandEnd ORDER BY abs(Distance-$bandMiddle) LIMIT 1", "")
		$output = $output + ";$cacheData"
	ENDIF
	$output = $output + $_CrLf
ENDSUB

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Thu 07-Feb-2013 20:38:47
#********************************************************************

Name = Form1
  Type = Form
  Height = 360
  Width = 311

Name = bandsGroupbox
  Type = Groupbox
  Caption = Bands
  Height = 106
  Left = 25
  Top = 20
  Width = 256
  Taborder = 0

Name = outputGroupbox
  Type = Groupbox
  Caption = Output
  Height = 141
  Left = 25
  Top = 140
  Width = 256
  Taborder = 1

Name = bandSizeStr
  Type = Edit
  Container = bandsGroupbox
  Height = 21
  Left = 120
  Top = 21
  Width = 51
  Taborder = 0

Name = minDistanceStr
  Type = Edit
  Container = bandsGroupbox
  Height = 21
  Left = 120
  Top = 48
  Width = 51
  Taborder = 1

Name = maxDistanceStr
  Type = Edit
  Container = bandsGroupbox
  Height = 21
  Left = 120
  Top = 75
  Width = 51
  Taborder = 2

Name = bandSizeLabel
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 15
  Top = 24
  Width = 101
  Caption = Distance Band Size

Name = minDistanceLabel
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 15
  Top = 51
  Width = 94
  Caption = Minimum Distance

Name = maxDistanceLabel
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 15
  Top = 78
  Width = 97
  Caption = Maximum Distance

Name = bandSizeUnits
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 180
  Top = 24
  Width = 55
  Caption = kilometers

Name = minDistanceUnits
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 180
  Top = 51
  Width = 55
  Caption = kilometers

Name = maxDistanceUnits
  Type = Label
  Container = bandsGroupbox
  Height = 17
  Left = 180
  Top = 78
  Width = 55
  Caption = kilometers

Name = includeQuantity
  Type = Checkbox
  Captionposition = Left
  Container = outputGroupbox
  Height = 20
  Left = 84
  Top = 15
  Width = 99
  Taborder = 0
  Caption = Include Quantity

Name = includePercent
  Type = Checkbox
  Captionposition = Left
  Container = outputGroupbox
  Height = 20
  Left = 62
  Top = 35
  Width = 121
  Taborder = 1
  Caption = Include Percentages

Name = includeCache
  Type = Checkbox
  Captionposition = Left
  Container = outputGroupbox
  Height = 20
  Left = 53
  Top = 55
  Width = 130
  Taborder = 2
  Caption = Include Sample Cache

Name = clipboardLabel
  Type = Label
  Container = outputGroupbox
  Height = 31
  Left = 25
  Top = 90
  Width = 203
  Caption = The report will be copied to the clipboard,<br>suitable for pasting into your cache log.

Name = runButton
  Type = Button
  Height = 25
  Left = 70
  Top = 290
  Width = 75
  Taborder = 4
  Caption = Run

Name = cancelButton
  Type = Button
  Escape = Yes
  Height = 25
  Left = 170
  Top = 290
  Width = 75
  Taborder = 5
  Caption = Cancel

<enddata>



